//===================================================================
// *** AGS ROOM SCRIPT ***
//
// Project: Demo Quest 3 
//          Source of Dreams
//
// Room:    Intro Text
//              
// Author:  Allan Menefe (Holister_Man)
//          Rick Jafrate (RickJ)    
//
// Abstract
// This room demonstartes an alternative use of the "Animating 
// background scenes" feature. Normally each room uses a single 
// background image. Up to four additional background images can be
// imported into a room.The AGS game engine cycles through each image
// image giving the effect of animation. The time between each images 
// is specified in Room Editor Settings as Animation Delay in terms 
// of game cycles. 
// 
// In this example each animation frame contains a different text 
// screen each of which are displayed sequentially.  The Animation
// Delay is set to a high value so that it does not interfere with 
// the following script program. The script program below performs
// fade-out, fade-in, and backgorund frame change operations.  
//
// Revision History
// 05-Mar-15 RickJ,        update for AGS 2.7
// 04-Dec-01 Holister_Man, original version
// 
// Copyright (c) 2005 by Chris Jones
//-------------------------------------------------------------------    


//===================================================================
// Constant Definitions
//-------------------------------------------------------------------


//===================================================================
// Static Variable Definitions
//-------------------------------------------------------------------
int PartCounter;


//===================================================================
// Utility Functions
//-------------------------------------------------------------------


//===================================================================
// Application Functions
//-------------------------------------------------------------------
//===================================================================
	function IntroInitFrame() {
// 
// This function hides the normal game GUI and initializes the screen
// to the first page of text.
//-------------------------------------------------------------------
	gButtonbar.Visible = false;
	mouse.Mode = eModePointer;
	PartCounter = 1;
	SetBackgroundFrame(0);
}

//===================================================================
	function IntroSetFrame(int newframe) {
//
// This function changes from one page of text to another as given by
// the parameter NEWFRAME. If the value of NEWFRAME is  between  zero 
// and four then the  corresponding page of text is displayed. If not
// the the next page after the current on is displayed. 
//-------------------------------------------------------------------
	int frame;

	// Determine frame number to display 
	if ((newframe<0)||(newframe>4)) { 		// If NEWFRAME is not valid (i.e. 0..4) 
		frame = GetBackgroundFrame() + 1;	// increment frame number
	}   
	else {											// Otherwise use NEWFRAME
		frame = newframe;
	}

	// Display the background frame given by the int variable frame
	FadeOut(1);
	if (frame==4) {                    		// If last frame, return to menu
		player.ChangeRoom(crmCredits);
	}
	else {                             		// Otherwise, dsiplay the next frame
		SetBackgroundFrame(frame);
		Wait(40);
		FadeIn(1);
	}
}


//===================================================================
// Room - Interaction Handler Functions
//-------------------------------------------------------------------
#sectionstart room_c  // DO NOT EDIT OR REMOVE THIS LINE
function room_c() {
	// script for room: Player enters screen (before fadein)
	IntroInitFrame();
}
#sectionend room_c  // DO NOT EDIT OR REMOVE THIS LINE

#sectionstart hotspot1_a  // DO NOT EDIT OR REMOVE THIS LINE
function hotspot1_a() {
	// script for hotspot1: Any click on hotspot
	IntroSetFrame(NEXT);
}
#sectionend hotspot1_a  // DO NOT EDIT OR REMOVE THIS LINE

